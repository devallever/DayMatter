apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "org.xm.app.daymatter"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        //默认渠道为官网
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "offical"]

        //在Manifest.xml中使用的值
        manifestPlaceholders += [
                ADMOB_APP_ID: rootProject.ext.admobAppId
        ]

    }

    //友盟多渠道
    flavorDimensions "mark"
    productFlavors {
        ad {}
//        google {}
//        xiaomi {}
//        baidu {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    //解决butterknife编译异常
    //Failed to transform artifact 'butterknife-runtime.aar (com.jakewharton:butterknife-runtime:10.1.0)' to match attributes {artifactType=android-dex, dexing-is-debuggable=true, dexing-min-sdk=19}
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file("${rootProject.ext.signingStoreFile}")
            storePassword "${rootProject.ext.signingStorePassword}"
            keyAlias "${rootProject.ext.signingKeyAlias}"
            keyPassword "${rootProject.ext.signingKeyPassword}"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }



    applicationVariants.all {
        variant ->
            if (variant.buildType.name.equals('release')) {
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "${defaultConfig.applicationId}_(Release)_V${defaultConfig.versionName}_C${defaultConfig.versionCode}_(Build${releaseTime()}).apk"
                        output.outputFileName = new File(fileName)
                        //com.tcqmt.xiaoxue_(Release)_V1.0_C1_(Build201905070905).apk
                    }
                }
            }
    }
}


def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("Asia/Chongqing"))
}

//引入aar包需加
repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    //Butterknife
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'
    //消息事件
    implementation 'org.greenrobot:eventbus:3.1.1'
    //数据库
    implementation 'org.litepal.android:core:1.6.1'
    //EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'
    //Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'
    //Quick Recycler Adapter
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.40'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //功能性RecyclerView
    implementation ('com.yanzhenjie.recyclerview:support:1.3.2') {
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }


    //local
    implementation project(':permission')
    implementation project(':common')
    implementation project(':umeng')
    implementation project(':admob')
    implementation(name: "Toolkit-1.1", ext: 'aar')

}
repositories {
    mavenCentral()
}
